# Maintainer: d0ku <darthtyranus666666 gmail com>

# Based on:
# Maintainer: Jose Riha <jose1711 gmail com>
# Contributor: Patrick Jackson <PatrickSJackson gmail com>
# Contributor: Christoph Vigano <mail@cvigano.de>

pkgname=st_custom
realname=st
pkgver=0.8.1
pkgrel=2
pkgdesc='A simple virtual terminal emulator for X.'
arch=('i686' 'x86_64' 'armv7h')
license=('MIT')
depends=('libxft' 'libxext' 'xorg-fonts-misc')
makedepends=('ncurses')
conflicts=('st')
_patches=("https://st.suckless.org/patches/anysize/st-anysize-0.8.1.diff" 
            "https://st.suckless.org/patches/hidecursor/st-hidecursor-0.8.1.diff"
"https://st.suckless.org/patches/scrollback/st-scrollback-0.8.1.diff"
"https://st.suckless.org/patches/scrollback/st-scrollback-mouse-0.8.diff"
"https://st.suckless.org/patches/scrollback/st-scrollback-mouse-altscreen-20190131-e23acb9.diff"
            )
url="http://st.suckless.org"
source=(http://dl.suckless.org/st/$realname-$pkgver.tar.gz config.h ${_patches[@]})

prepare() {
  # user is supposed to maintain config.h him/herself
  cp $srcdir/config.h $srcdir/$realname-$pkgver/config.h
  cp $srcdir/*.diff $srcdir/$realname-$pkgver
  cd $srcdir/$realname-$pkgver
  for patch in "${_patches[@]}"; do
    echo "Applying patch $(basename $patch)..."
    patch -Np1 -i "$(basename $patch)"
  done
  cd -
}

build() {
  cd $srcdir/$realname-$pkgver
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd $srcdir/$realname-$pkgver
  make PREFIX=/usr DESTDIR="$pkgdir" TERMINFO="$pkgdir/usr/share/terminfo" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$realname/LICENSE"
  install -Dm644 README "$pkgdir/usr/share/doc/$realname/README"
  # remove to avoid conflict with ncurses
  rm "${pkgdir}/usr/share/terminfo/s/st" "${pkgdir}/usr/share/terminfo/s/st-256color" 
}
md5sums=('92135aecdba29300bb2e274a55f5b71e'
         '81c696f52543e7ad7e065549e60d6680'
         '9effc30b79e07c3dc9d2a1e41b9dd6d1'
         '7b6f4a1efe7987b8f5554b4ec16082f9'
         '244efdbbd3dc00e8fe8d607fc5ed004b'
         '72227737f6cd831afd014a3613bd559d'
         '1b41ef8af1f2ccf3605fb5da5438e5ee')
